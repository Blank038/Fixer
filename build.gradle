buildscript {
    repositories {
        jcenter()
        maven {
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'net.minecraftforge.gradle.forge'

    minecraft {
        version = "1.12.2-14.23.5.2807"
        runDir = "run"
        mappings = "snapshot_20171003"
    }

    version = "1.2.8-SNAPSHOT"
    group = "com.blank038.fixer"
    archivesBaseName = "Fixer"


    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    sourceCompatibility = targetCompatibility = '1.8'
    compileJava {
        sourceCompatibility = targetCompatibility = '1.8'
    }
}

project(":Common") {
    processResources {
        inputs.property "version", project.version

        from(sourceSets.main.resources.srcDirs) {
            include 'plugin.yml'

            expand 'version': project.version, 'name': project.name
        }

        from(sourceSets.main.resources.srcDirs) {
            exclude 'plugin.yml'
        }
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path)}

task allJar(type: Jar, dependsOn: subprojects.jar) {
    baseName = 'Fixer.M'
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

artifacts {
    archives allJar
}